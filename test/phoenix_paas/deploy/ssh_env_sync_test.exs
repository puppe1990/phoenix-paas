defmodule PhoenixPaas.Deploy.SshEnvSyncTest do
  use PhoenixPaas.DataCase, async: false

  alias PhoenixPaas.Apps
  alias PhoenixPaas.Apps.App
  alias PhoenixPaas.Deploy.Ssh
  alias PhoenixPaas.TenancyFixtures

  setup do
    scope = TenancyFixtures.scope_fixture()
    server = TenancyFixtures.server_fixture(scope)

    {:ok, app, _webhook_status} =
      Apps.create_app(scope, %{
        name: "Trip Planner",
        slug: "trip-planner",
        github_repo: "puppe1990/trip-planner-ia-phx",
        host: "trip.gestaobem.com",
        server_id: server.id
      })

    %{app: app}
  end

  test "env_sync_enabled? is false without stored env vars", %{app: app} do
    app = Apps.get_app!(app.id)
    refute Ssh.env_sync_enabled?(app)
  end

  test "env_sync_enabled? is true when env vars exist", %{app: app} do
    {:ok, _} = Apps.put_env_var(app, "SECRET_KEY_BASE", "super-secret")
    app = Apps.get_app!(app.id)

    assert Ssh.env_sync_enabled?(app)
  end

  test "env_file_content includes PHX_HOST and stored vars", %{app: app} do
    {:ok, _} = Apps.put_env_var(app, "SECRET_KEY_BASE", "super-secret")
    {:ok, _} = Apps.put_env_var(app, "GEMINI_API_KEY", "gemini-key")
    app = Apps.get_app!(app.id)

    assert Ssh.env_file_content(app) ==
             """
             GEMINI_API_KEY=gemini-key
             PHX_HOST=trip.gestaobem.com
             SECRET_KEY_BASE=super-secret
             """
  end

  test "env_sync_script writes base64 env file on remote host", %{app: app} do
    {:ok, _} = Apps.put_env_var(app, "SECRET_KEY_BASE", "super-secret")
    app = Apps.get_app!(app.id)
    config = App.deploy_config(app)
    script = Ssh.env_sync_script(app, config)

    assert script =~ "Syncing environment from panel"
    assert script =~ "/etc/trip_planner_ia/env"
    assert script =~ Base.encode64(Ssh.env_file_content(app))
  end

  test "env_sync_script is empty when no env vars are stored", %{app: app} do
    app = Apps.get_app!(app.id)
    config = App.deploy_config(app)

    assert Ssh.env_sync_script(app, config) == ""
  end
end
